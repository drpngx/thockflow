module(
    name = "thockflow",
    version = "0.0.0",
    compatibility_level = 1,
)

bazel_dep(name = "rules_python", version = "1.7.0")

python = use_extension("@rules_python//python/extensions:python.bzl", "python")

kPythonVersion = "3.12"

python.toolchain(
    is_default = True,
    python_version = kPythonVersion,
)

pip = use_extension("@rules_python//python/extensions:pip.bzl", "pip")
pip.parse(
    hub_name = "pip_deps",
    python_version = kPythonVersion,
    requirements_lock = "//:requirements_lock.txt",
)
use_repo(pip, "pip_deps")

bazel_dep(name = "rules_oci", version = "2.2.6")

oci = use_extension("@rules_oci//oci:extensions.bzl", "oci")
oci.pull(
    name = "distroless_base",
    image = "gcr.io/distroless/cc",
    platforms = [
        "linux/amd64",
        "linux/arm64/v8",
    ],
    # 'latest' is not reproducible, but it's convenient.
    # During the build we print a WARNING message that includes recommended 'digest' and 'platforms'
    # values which you can use here in place of 'tag' to pin for reproducibility.
    tag = "latest",
)
use_repo(oci, "distroless_base", "distroless_base_linux_amd64", "distroless_base_linux_arm64_v8")

# Nonroot variant for rootless container runtimes (Podman, etc.)
oci.pull(
    name = "distroless_base_nonroot",
    image = "gcr.io/distroless/cc",
    platforms = ["linux/amd64"],
    tag = "nonroot",
)
use_repo(oci, "distroless_base_nonroot", "distroless_base_nonroot_linux_amd64")

bazel_dep(name = "hermetic_cc_toolchain", version = "4.0.0")
git_override(
    module_name = "hermetic_cc_toolchain",
    commit = "e906f270fa38c1fe0b2db346717d2f2cd90da123",
    remote = "https://github.com/uber/hermetic_cc_toolchain.git",
)

bazel_dep(name = "rules_rust", version = "0.64.0")
bazel_dep(name = "platforms", version = "1.0.0")

RUST_EDITION = "2024"

RUST_VERSION = "1.89.0"

rust = use_extension("@rules_rust//rust:extensions.bzl", "rust")
rust.toolchain(
    edition = RUST_EDITION,
    extra_target_triples = [
        "aarch64-unknown-linux-gnu",
        "x86_64-unknown-linux-gnu",
        "wasm32-unknown-unknown",
    ],
    versions = [RUST_VERSION],
)

# Register hermetic C/C++ toolchains (Zig) for cross-linking Linux targets.
# With bzlmod, toolchains from dependencies are not auto-registered in the root
# module unless we explicitly do so.
hermetic_cc = use_extension(
    "@hermetic_cc_toolchain//toolchain:ext.bzl",
    "toolchains",
)
use_repo(hermetic_cc, "zig_sdk")

register_toolchains(
    # Linux glibc toolchains commonly needed for server images
    "@zig_sdk//toolchain:linux_amd64_gnu.2.28",
    "@zig_sdk//toolchain:linux_arm64_gnu.2.28",
)

crate_index = use_extension("@rules_rust//crate_universe:extensions.bzl", "crate")
crate_index.from_cargo(
    name = "crate_index",
    cargo_lockfile = "//:Cargo.lock",
    generate_binaries = True,
    manifests = [
        "//:Cargo.toml",
    ],
)
use_repo(
    crate_index,
    "crate_index",
)

rust_host_tools = use_extension("@rules_rust//rust:extensions.bzl", "rust_host_tools")
rust_host_tools.host_tools(
    name = "rust_host_tools_nightly",
    version = "nightly",
)
use_repo(
    rust_host_tools,
    "rust_host_tools_nightly",
)

bazel_dep(name = "rules_rust_wasm_bindgen", version = "0.64.0")

# use manual wasm-bindgen toolchain to fix windows build failure
bindeps = use_extension("@rules_rust//crate_universe:extension.bzl", "crate")
bindeps.spec(
    artifact = "bin",
    package = "wasm-bindgen-cli",
    version = "=0.2.100",
)
bindeps.annotation(
    crate = "wasm-bindgen-cli",
    gen_all_binaries = True,
)
bindeps.from_specs(
    name = "bindeps",
    host_tools = "@rust_host_tools_nightly",
)
use_repo(bindeps, "bindeps")

register_toolchains("//toolchains:wasm_bindgen_toolchain")

bazel_dep(name = "rules_pkg", version = "1.0.1")

#### JAVASCRIPT ####

# for jq as well
# Much of this was inspired by the bazel react-frontend example.
bazel_dep(name = "aspect_bazel_lib", version = "2.21.0")
bazel_dep(name = "aspect_rules_lint", version = "1.2.2")
bazel_dep(name = "aspect_rules_jest", version = "0.22.0")
bazel_dep(name = "aspect_rules_js", version = "2.5.0")
bazel_dep(name = "aspect_rules_swc", version = "2.4.4")
bazel_dep(name = "aspect_rules_ts", version = "3.1.0")
bazel_dep(name = "aspect_rules_rollup", version = "2.0.1")
bazel_dep(name = "aspect_rules_webpack", version = "0.16.0")
bazel_dep(name = "bazel_skylib", version = "1.8.1")
bazel_dep(name = "rules_nodejs", version = "6.5.0")

node = use_extension("@rules_nodejs//nodejs:extensions.bzl", "node", dev_dependency = True)
node.toolchain(node_version = "20.13.1")

pnpm = use_extension("@aspect_rules_js//npm:extensions.bzl", "pnpm", dev_dependency = True)
use_repo(pnpm, "pnpm")

npm = use_extension("@aspect_rules_js//npm:extensions.bzl", "npm", dev_dependency = True)
npm.npm_translate_lock(
    name = "app_npm",
    npmrc = "//bundle:.npmrc",
    pnpm_lock = "//bundle:pnpm-lock.yaml",
    verify_node_modules_ignored = "//:.bazelignore",
)
use_repo(npm, "app_npm")

rules_ts_ext = use_extension(
    "@aspect_rules_ts//ts:extensions.bzl",
    "ext",
    dev_dependency = True,
)
rules_ts_ext.deps()
use_repo(rules_ts_ext, "npm_typescript")

#### wasm compression
bazel_dep(name = "brotli", version = "1.1.0")

#### wasm-opt
bazel_dep(name = "emsdk", version = "4.0.13")

emscripten_deps = use_extension(
    "@emsdk//:emscripten_deps.bzl",
    "emscripten_deps",
)
use_repo(emscripten_deps, "emscripten_bin_linux")
use_repo(emscripten_deps, "emscripten_bin_linux_arm64")
use_repo(emscripten_deps, "emscripten_bin_mac")
use_repo(emscripten_deps, "emscripten_bin_mac_arm64")
use_repo(emscripten_deps, "emscripten_bin_win")

# Register local emsdk toolchains (wasm-opt) so our custom rule can find them
register_toolchains(
    "//emsdk:emsdk_mac_arm64_toolchain",
    "//emsdk:emsdk_mac_toolchain",
    "//emsdk:emsdk_linux_toolchain",
    "//emsdk:emsdk_win_toolchain",
)
